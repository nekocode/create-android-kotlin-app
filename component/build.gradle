apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.dcendents.android-maven'

group='com.github.nekocode'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 127
        versionName projectVersion
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    // Android support library
    provided "com.android.support:appcompat-v7:$supportLibVersion"
    provided "com.android.support:recyclerview-v7:$supportLibVersion"

    // Reactive library
    provided "io.reactivex:rxkotlin:0.40.1"
    provided "io.reactivex:rxandroid:1.1.0"

    // DSL
    provided 'org.jetbrains.anko:anko-sdk15:0.8.3' // sdk19, sdk21, sdk23 are also available
    provided 'org.jetbrains.anko:anko-support-v4:0.8.3' // In case you need support-v4 bindings
    provided 'org.jetbrains.anko:anko-appcompat-v7:0.8.3' // For appcompat-v7 bindings

    provided kotlib
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.getDestinationDir()
}

artifacts {
    archives sourcesJar
    archives javadocJar
}